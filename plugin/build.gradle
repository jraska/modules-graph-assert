plugins {
  id "com.gradle.plugin-publish" version "1.3.1"
  id "java-gradle-plugin"
}

apply plugin: 'kotlin'

repositories {
  mavenCentral()
}

dependencies {
  implementation gradleApi()
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  testImplementation 'junit:junit:4.13.2'
}

compileKotlin {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11
  }
}
java {
  sourceCompatibility JavaVersion.VERSION_11
  targetCompatibility JavaVersion.VERSION_11
}

group = 'com.jraska.module.graph.assertion'

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_URL

  plugins {
    modulesGraphAssert {
      id = 'com.jraska.module.graph.assertion'
      version = '2.7.3'
      displayName = POM_NAME
      description = POM_DESCRIPTION
      implementationClass = 'com.jraska.module.graph.assertion.ModuleGraphAssertionsPlugin'
      tags.addAll('graph', 'assert', 'build speed', 'android', 'java', 'kotlin', 'quality', 'multiprojects', 'module')
    }
  }
}

afterEvaluate {
  publishing.publications.forEach {
    it.pom {
      name = POM_NAME
      description = POM_DESCRIPTION
      url = POM_URL
      licenses {
        license {
          name = POM_LICENCE_NAME
          url = POM_LICENCE_URL
        }
      }
      scm {
        connection = POM_SCM_CONNECTION
        developerConnection = POM_SCM_DEV_CONNECTION
        url = POM_SCM_URL
      }
    }
  }
}
